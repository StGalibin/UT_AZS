#Область ОписаниеПеременных

&НаКлиенте
Перем МассивТикетовСбербанк; // хранит массив тикетов отправленных запросов

&НаКлиенте
Перем ТикетСбербанк; // тикет на запрос статусов запросов выписки

&НаКлиенте
Перем ОповещениеПослеПолученияОтветаПоТикетуСбербанк; // хранит оповещение, которое должно быть вызвано после получения ответа по тикету

&НаКлиенте
Перем СертификатПодписиСбербанк; // сертификат, с использованием которого подписываются запросы новых документов

&НаКлиенте
Перем ИдентификаторСертификатаСбербанк; // идентификатор сертификата подписи

&НаКлиенте
Перем ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк; // параметры подключаемой процедуры при ожидании конечного статуса запросов выписки

&НаКлиенте
Перем ИтогКолОтправленных; // Количество отправленных документов

&НаКлиенте
Перем ИтогКолПолученных; // Количество полученных документов

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		НастройкаОбмена, "Организация, Банк, ПрограммаБанка, ИдентификаторОрганизации, ИмяВнешнегоМодуля");
	
	ПрограммаБанка = РеквизитыНастройкиОбмена.ПрограммаБанка;
	БИК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыНастройкиОбмена.Банк, "Код");
	ИмяВнешнегоМодуля = РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля;
	ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
	ИсходныйТикетСбербанк = Параметры.ИсходныйТикетСбербанк;
	
	Если Параметры.ВидОперации = "ПолучениеВыписки" Тогда
		МассивБанковскихСчетов = Новый Массив;
		Если ЗначениеЗаполнено(Параметры.НомерСчета) Тогда
			МассивБанковскихСчетов.Добавить(Параметры.НомерСчета);
		Иначе
			ОбменСБанкамиПереопределяемый.ПолучитьНомераБанковскихСчетов(
				РеквизитыНастройкиОбмена.Организация, РеквизитыНастройкиОбмена.Банк, МассивБанковскихСчетов);
		КонецЕсли;
		
		МассивБанковскихСчетовСсылка = ПоместитьВоВременноеХранилище(МассивБанковскихСчетов, УникальныйИдентификатор);
		Заголовок = НСтр("ru = 'Получение выписки'");
		
		// Если пришли запросы выписок Сбербанка, то необходимо получить тексты запросов на сервере.
		Если ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн И Параметры.ГотовыеВыписки.Количество() = 0 Тогда
			ДанныеЗапросовВыписокСбербанк = ДанныеЗапросовВыписокСбербанк(Параметры.МассивСообщенийОбмена);
			ТекстыЗапросовВыписокСбербанк = ПоместитьВоВременноеХранилище(ДанныеЗапросовВыписокСбербанк, УникальныйИдентификатор);
			ДатаНачалаОперацииСбербанк = ТекущаяДатаСеанса();
		КонецЕсли;
	ИначеЕсли Параметры.ВидОперации = "ВыполнениеСинхронизации" Тогда
		Заголовок = НСтр("ru = 'Выполнение синхронизации'");
	ИначеЕсли Параметры.ВидОперации = "ПолучениеСостоянияДокумента" Тогда
		Заголовок = НСтр("ru = 'Получение состояния электронного документа'");
	ИначеЕсли Параметры.ВидОперации = "Тестирование" Тогда
		Заголовок = НСтр("ru = 'Тестирование обмена данными'")
	КонецЕсли;
	
	СообщениеОбмена = Параметры.СообщениеОбмена;
	Пользователь = Параметры.Пользователь;
	Пароль = Параметры.Пароль;
	ИдентификаторСессии = Параметры.ИдентификаторСессии;
	ДатаОкончания = Параметры.ДатаОкончания;
	ДатаНачала = Параметры.ДатаНачала;
	ГотовыеВыпискиСсылка =  ПоместитьВоВременноеХранилище(Параметры.ГотовыеВыписки, УникальныйИдентификатор);
	ДанныеСертификатаСсылка = ПоместитьВоВременноеХранилище(Параметры.ДанныеСертификата, УникальныйИдентификатор);
	ВидОперации = Параметры.ВидОперации;
	МассивСообщенийОбменаСсылка = ПоместитьВоВременноеХранилище(Параметры.МассивСообщенийОбмена, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Перем ЗапросОтправлен, ВыпискаБанка;
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		Если ВидОперации = "ПолучениеВыписки" Тогда
			ПодключитьОбработчикОжидания("НачатьОтправкуЗапросаВыпискиВБанк", 0.1, Истина);
		ИначеЕсли ВидОперации = "ПолучениеСостоянияДокумента" Тогда
			ПодключитьОбработчикОжидания("НачатьОтправкуЗапросаСтатусаЭД", 0.1, Истина);
		Иначе
			ПодключитьОбработчикОжидания("НачатьОтправкуЗапросаЗонда", 0.1, Истина);
		КонецЕсли
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		ОбменСБанкамиСлужебныйКлиент.ПроцессПрерван(НастройкаОбмена);
		Если ВидОперации = "ПолучениеСостоянияДокумента" Тогда

			ПолучитьСостояниеЭДЧерезВК();
		Иначе
			ПолучитьВыпискуЧерезВК();
		КонецЕсли;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ПолучитьВыпискуЧерезДополнительнуюОбработку();
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		Если ВидОперации = "ПолучениеСостоянияДокумента" Тогда
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("ПолучитьИзвещениеОСостоянииДокументаСбербанк", 0.1, Истина);
		ИначеЕсли ВидОперации = "ВыполнениеСинхронизации" Тогда
			ПодключитьОбработчикОжидания("ПолучитьНовыеДокументыСбербанк", 0.1, Истина);
		Иначе // получение выписки
			ГотовыеВыписки = ПолучитьИзВременногоХранилища(ГотовыеВыпискиСсылка);
			Если ГотовыеВыписки.Количество() Тогда
				ОповеститьОВыборе(ГотовыеВыписки);
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			ПодключитьОбработчикОжидания("НачатьОтправкуЗапросовВыписокСбербанк", 0.1, Истина);
		КонецЕсли;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
		ПодключитьОбработчикОжидания("НачатьОтправкуЗапросаВыпискиВБанк", 0.1, Истина);
	КонецЕсли;
	
	Если ЗакрытьФорму Тогда
		Отказ = Истина;
	Иначе
		ФормаОткрыта = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ПроизошлаОшибка Тогда
		Отказ = Истина;
		ПроизошлаОшибка = Ложь;
		Элементы.Страницы.ТекущаяСтраница = Элементы.Ошибка;
		Элементы.ФормаОтмена.Заголовок = "Закрыть";
		
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			ОтключитьОбработчикОжидания("Подключаемый_ПолучитьНовыеДокументыСбербанк");
		КонецЕсли;
	Иначе
		ФормаОткрыта = Ложь;
	КонецЕсли;
	
	ОбменСБанкамиСлужебныйКлиент.ПрерватьПроцессыНаКлиенте(НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ПриЗакрытииНаСервере(ИдентификаторЗадания);
	КонецЕсли;
	
	ФормаОткрыта = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьОтправкуЗапросаВыпискиВБанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыЗапроса.Вставить("Пользователь", Пользователь);
	ПараметрыЗапроса.Вставить("Пароль", Пароль);
	
	ОперацияВыполнена = ОтправитьЗапросВыпискиНаСервере(
		МассивСообщенийОбменаСсылка, ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания);
	
	Если ОперацияВыполнена Тогда
		ОбработатьРезультатЗапросаВыписки();
		Возврат;
	КонецЕсли;
	
	// Операция еще не завершена, выполняется с помощью фонового задания (асинхронно).
	
	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);

	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияЗапросаВыписки", 1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура НачатьОтправкуЗапросаСтатусаЭД()
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("СообщениеОбмена", СообщениеОбмена);
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОперацияВыполнена = ОтправитьЗапросСостоянияЭДНаСервере(
		ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания, ПроизошлаОшибка);
	
	Если ОперацияВыполнена Тогда
		ОбработатьРезультатЗапросаСостоянияЭД();
		Возврат;
	КонецЕсли;
	
	// Операция еще не завершена, выполняется с помощью фонового задания (асинхронно).
	
	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияЗапросаСостоянияЭД", 1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура НачатьОтправкуЗапросаЗонда()
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("СообщениеОбмена", СообщениеОбмена);
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	МассивСообщений = Новый Массив;
	ОперацияВыполнена = ОбменСБанкамиСлужебныйВызовСервера.ОтправитьЗапросЗондНаСервере(
		ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания, МассивСообщений);
		
	Если МассивСообщений.Количество() Тогда //Произошла ошибка
		СтруктураВозврата = Новый Структура("Успех, МассивСообщений", Ложь, МассивСообщений);
		Если ФормаОткрыта Тогда
			Закрыть(СтруктураВозврата);
		Иначе
			Если Не ОписаниеОповещенияОЗакрытии = Неопределено Тогда
				ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, СтруктураВозврата);
			КонецЕсли;
			ЗакрытьФорму = Истина;
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	Если ОперацияВыполнена Тогда
		ОбработатьРезультатЗапросаЗонда();
		Возврат;
	КонецЕсли;
	
	// Операция еще не завершена, выполняется с помощью фонового задания (асинхронно).
	
	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияОтправкиЗапросаЗонда", 1, Истина);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтправитьЗапросВыпискиНаСервере(Знач МассивЗапросовСсылка, Знач ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания)
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	ПараметрыЗапроса.Вставить("МассивСообщенийОбмена", ПолучитьИзВременногоХранилища(МассивЗапросовСсылка));
	
	ЗаданиеВыполнено = Ложь;
		
	НаименованиеЗадания = НСтр("ru = 'Отправка запроса выписки в банк'");
	ПараметрыВыполнения = Новый Массив;
	ПараметрыВыполнения.Добавить(ПараметрыЗапроса);
	ПараметрыВыполнения.Добавить(АдресХранилища);
		
	Если ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
		ВремяОжидания = 4;
	Иначе
		ВремяОжидания = 2;
	КонецЕсли;
	
	Задание = ФоновыеЗадания.Выполнить(
		"ОбменСБанкамиСлужебный.ОтправитьЗапросВыпискиВБанк", ПараметрыВыполнения, , НаименованиеЗадания);
	Попытка
		Задание.ОжидатьЗавершения(ВремяОжидания);
	Исключение
		// Специальная обработка не требуется. Предположительно, исключение вызвано истечением времени ожидания.
	КонецПопытки;

	ИдентификаторЗадания = Задание.УникальныйИдентификатор;
	// Если операция уже завершилась, то сразу обрабатываем результат.
	Если ДлительныеОперации.ЗаданиеВыполнено(Задание.УникальныйИдентификатор) Тогда
		ЗаданиеВыполнено = Истина;
	КонецЕсли;
	Возврат ЗаданиеВыполнено;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОтправитьЗапросСостоянияЭДНаСервере(Знач ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания, ЕстьОшибка)
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ЗаданиеВыполнено = Ложь;
		
	НаименованиеЗадания = НСтр("ru = 'Отправка запроса состояния ЭД в банк'");
	ПараметрыВыполнения = Новый Массив;
	ПараметрыВыполнения.Добавить(ПараметрыЗапроса);
	ПараметрыВыполнения.Добавить(АдресХранилища);
		
	Если ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
		ВремяОжидания = 4;
	Иначе
		ВремяОжидания = 2;
	КонецЕсли;
	
	Задание = ФоновыеЗадания.Выполнить(
		"ОбменСБанкамиСлужебный.ОтправитьЗапросСостоянияЭДВБанк", ПараметрыВыполнения, , НаименованиеЗадания);
	Попытка
		Задание.ОжидатьЗавершения(ВремяОжидания);
	Исключение
		// Специальная обработка не требуется. Предположительно, исключение вызвано истечением времени ожидания.
	КонецПопытки;

	ИдентификаторЗадания = Задание.УникальныйИдентификатор;
	// Если операция уже завершилась, то сразу обрабатываем результат.
	Если ДлительныеОперации.ЗаданиеВыполнено(Задание.УникальныйИдентификатор) Тогда
		ЗаданиеВыполнено = Истина;
	КонецЕсли;
	Возврат ЗаданиеВыполнено;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПриЗакрытииНаСервере(Знач ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияЗапросаСостоянияЭД()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			ОбработатьРезультатЗапросаСостоянияЭД();
			Возврат;
		КонецЕсли;
	Исключение
		ВызватьИсключение;
	КонецПопытки;

	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
													* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	ПодключитьОбработчикОжидания(
		"Подключаемый_ПроверитьВыполнениеЗаданияЗапросаСостоянияЭД", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияОтправкиЗапросаЗонда()

	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			ОбработатьРезультатЗапросаЗонда();
			Возврат;
		КонецЕсли;
	Исключение
		ВызватьИсключение;
	КонецПопытки;

	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
													* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	ПодключитьОбработчикОжидания(
		"Подключаемый_ПроверитьВыполнениеЗаданияОтправкиЗапросаЗонда", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияЗапросаВыписки()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			ОбработатьРезультатЗапросаВыписки();
			Возврат;
		КонецЕсли;
	Исключение
		ВызватьИсключение;
	КонецПопытки;

	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
													* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	ПодключитьОбработчикОжидания(
		"Подключаемый_ПроверитьВыполнениеЗаданияЗапросаВыписки", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапросаВыписки()

	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
		ОбработатьРезультатПолученияВыпискиСинхронныйОбмен();
		Возврат;
	КонецЕсли;
	
	МассивСообщений = ОбменСБанкамиСлужебныйВызовСервера.СообщенияОбОшибкахФоновогоЗадания(ИдентификаторЗадания);
	
	Если МассивСообщений.Количество() Тогда
		
		Для Каждого СообщениеОбОшибке Из МассивСообщений Цикл
			СообщениеОбОшибке.Сообщить();
		КонецЦикла;
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Оповестить("ОбновитьСостояниеОбменСБанками");
		Возврат;
	КонецЕсли;

	ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк'");
	
	ТекстОповещения = НСтр("ru = 'Отправлено документов: (1)'");
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	
	Если НЕ ПроизошлаОшибка Тогда
		
		ПараметрыОжиданияПолученияВыписки = Новый Структура();
		ПараметрыОжиданияПолученияВыписки.Вставить("МинимальныйИнтервал", 1);
		ПараметрыОжиданияПолученияВыписки.Вставить("МаксимальныйИнтервал", 15);
		ПараметрыОжиданияПолученияВыписки.Вставить("ТекущийИнтервал", 1);
		ПараметрыОжиданияПолученияВыписки.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
		
		ПодключитьОбработчикОжидания("Подключаемый_ПолучитьВыпискуАсинхронныйОбмен", 1, Истина);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолученияВыпискиСинхронныйОбмен()
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если СтруктураВозврата.Свойство("ДанныеЭП") И СтруктураВозврата.ДанныеЭП.Количество() Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПослеСохраненияПолученныхПодписейВыпискиСинхронныйОбмен", ЭтотОбъект);
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДанныеЭП", СтруктураВозврата.ДанныеЭП);
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Ложь);
		ДополнительныеПараметры.Вставить("ОповещениеПослеСохраненияПодписей", Оповещение);
		
		СохранитьОчереднуюПодпись(ДополнительныеПараметры);
		
		Возврат;
		
	КонецЕсли;
	
	ПослеСохраненияПолученныхПодписейВыпискиСинхронныйОбмен(Ложь, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПолученныхПодписейВыпискиСинхронныйОбмен(ЕстьОшибка, ДополнительныеПараметры) Экспорт
	
	МассивСообщений = ОбменСБанкамиСлужебныйВызовСервера.СообщенияОбОшибкахФоновогоЗадания(ИдентификаторЗадания);
	Если МассивСообщений.Количество() Тогда
		Для Каждого СообщениеОбОшибке Из МассивСообщений Цикл
			СообщениеОбОшибке.Сообщить();
		КонецЦикла;
		ПроизошлаОшибка = Истина;
		Если НЕ ФормаОткрыта Тогда
			ПриЗакрытииНаСервере(ИдентификаторЗадания);
			ЗакрытьФорму = Истина;
		Иначе
			Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ЕстьОшибка Тогда
		ПроизошлаОшибка = Истина;
		Если НЕ ФормаОткрыта Тогда
			ПриЗакрытииНаСервере(ИдентификаторЗадания);
			ЗакрытьФорму = Истина;
		Иначе
			Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	ВыпискаБанка = СтруктураВозврата.ВыпискаБанка;
	
	Если ВыпискаБанка = Неопределено Тогда // Произошла ошибка
		ПроизошлаОшибка = Истина;
		ОповеститьОВыборе(Неопределено);
	Иначе
		ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк'");
		ТекстОповещения = НСтр("ru = 'Получено документов: (1).'");
		Оповестить("ОбновитьСостояниеОбменСБанками");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
		ОповеститьОВыборе(ВыпискаБанка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьВыпискуАсинхронныйОбмен()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	ОперацияВыполнена = ПолучитьВыпискуАсинхронноНаСервере(
		ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания, ПроизошлаОшибка);
	
	Если ОперацияВыполнена Тогда
		ОбработатьРезультатПолученияВыписки();
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеПолученияВыписки", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапросаСостоянияЭД()

	МассивСообщений = ОбменСБанкамиСлужебныйВызовСервера.СообщенияОбОшибкахФоновогоЗадания(ИдентификаторЗадания);

	Если МассивСообщений.Количество() Тогда
		
		Для Каждого СообщениеОбОшибке Из МассивСообщений Цикл
			СообщениеОбОшибке.Сообщить();
		КонецЦикла;
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Оповестить("ОбновитьСостояниеОбменСБанками");
		Возврат;
	КонецЕсли;
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(АдресХранилища);

	ПараметрыЗапроса.Вставить("СообщениеОбмена", СтруктураВозврата.СообщениеОбменаЗапросСостояния);
	
	ОперацияВыполнена = ОбменСБанкамиСлужебныйВызовСервера.ПолучитьИзвещениеОСостоянииЭДАсинхронноНаСервере(
			ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания);
	
	Если ОперацияВыполнена Тогда
		ОбработатьРезультатПолученияИзвещенияОСостоянииЭД();
		Возврат;
	КонецЕсли;

	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеПолученияИзвещенияОСостоянии", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапросаЗонда()

	КолОтправленных = 0;
	КолПолученных = 0;
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	МассивСообщений = ОбменСБанкамиСлужебныйВызовСервера.СообщенияОбОшибкахФоновогоЗадания(ИдентификаторЗадания);
	
	Если МассивСообщений.Количество() Тогда
		СтруктураВозврата = Новый Структура("Успех, МассивСообщений", Ложь, МассивСообщений);
		Если Не ФормаОткрыта Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, СтруктураВозврата);
			ЗакрытьФорму = Истина;
		Иначе
			Закрыть(СтруктураВозврата);
		КонецЕсли;
		Возврат
	КонецЕсли;
	
	ОперацияВыполнена = ОбменСБанкамиСлужебныйВызовСервера.ПолучитьИзвещениеОСостоянииЭДАсинхронноНаСервере(
		ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания);
	
	Если ОперацияВыполнена Тогда
		ОбработатьРезультатПолученияИзвещенияОСостоянииЭД();
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеПолученияИзвещенияОСостоянии", 1, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВыпискуАсинхронноНаСервере(Знач ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания, ЕстьОшибка)
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ЗаданиеВыполнено = Ложь;
		
	НаименованиеЗадания = НСтр("ru = 'Получение выписки из банка'");
	ПараметрыВыполнения = Новый Массив;
	ПараметрыЗапроса.Вставить("АдресРезультата");
	ПараметрыВыполнения.Добавить(ПараметрыЗапроса);
	ПараметрыВыполнения.Добавить(АдресХранилища);
		
	Если ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
		ВремяОжидания = 4;
	Иначе
		ВремяОжидания = 2;
	КонецЕсли;
	
	Задание = ФоновыеЗадания.Выполнить(
		"ОбменСБанкамиСлужебный.ПолучитьВыпискуБанкаАсинхронно", ПараметрыВыполнения, , НаименованиеЗадания);
	Попытка
		Задание.ОжидатьЗавершения(ВремяОжидания);
	Исключение
		// Специальная обработка не требуется. Предположительно, исключение вызвано истечением времени ожидания.
	КонецПопытки;

	ИдентификаторЗадания = Задание.УникальныйИдентификатор;
	// Если операция уже завершилась, то сразу обрабатываем результат.
	Если ДлительныеОперации.ЗаданиеВыполнено(Задание.УникальныйИдентификатор) Тогда
		ЗаданиеВыполнено = Истина;
	КонецЕсли;
	Возврат ЗаданиеВыполнено;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеПолученияВыписки()

	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			ОбработатьРезультатПолученияВыписки();
			Возврат; // задание выполнили, выходим
		КонецЕсли;
	Исключение
		ВызватьИсключение;
	КонецПопытки;

	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
		* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	ПодключитьОбработчикОжидания(
		"Подключаемый_ПроверитьВыполнениеПолученияВыписки", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеПолученияИзвещенияОСостоянии()

	Если НЕ ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			ОбработатьРезультатПолученияИзвещенияОСостоянииЭД();
			Возврат;
		КонецЕсли;
	Исключение
		ВызватьИсключение;
	КонецПопытки;

	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
		* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеПолученияИзвещенияОСостоянии",
											ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолученияВыписки()
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	ОбменСБанкамиСлужебныйКлиент.ВызватьОповещения(СтруктураВозврата);
	Оповестить("ОбновитьСостояниеОбменСБанками", СтруктураВозврата.ПараметрОповещения);
	
	Если СтруктураВозврата.Свойство("ДанныеЭП") И СтруктураВозврата.ДанныеЭП.Количество() Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПослеСохраненияПолученныхПодписейВыписки", ЭтотОбъект);
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДанныеЭП", СтруктураВозврата.ДанныеЭП);
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Ложь);
		ДополнительныеПараметры.Вставить("ОповещениеПослеСохраненияПодписей", Оповещение);
		
		СохранитьОчереднуюПодпись(ДополнительныеПараметры);
		
		Возврат;
		
	КонецЕсли;
	
	ПослеСохраненияПолученныхПодписейВыписки(Ложь, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОчереднуюПодпись(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДанныеЭП.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСохраненияПодписей, ДополнительныеПараметры.ЕстьОшибка);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.ДанныеЭП Цикл
		Прервать;
	КонецЦикла;
	
	ДополнительныеПараметры.ДанныеЭП.Удалить(КлючЗначение.Ключ);
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияПодписейСообщенияОбмена", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ДобавитьПодписиИОпределитьСтатусы(Оповещение, КлючЗначение.Ключ, КлючЗначение.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПодписейСообщенияОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Истина);
	КонецЕсли;
	
	СохранитьОчереднуюПодпись(ДополнительныеПараметры)
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПолученныхПодписейВыписки(ЕстьОшибка, ДополнительныеПараметры) Экспорт
	
	МассивСообщений = ОбменСБанкамиСлужебныйВызовСервера.СообщенияОбОшибкахФоновогоЗадания(ИдентификаторЗадания);
	Если МассивСообщений.Количество() Тогда
		Для Каждого СообщениеОбОшибке Из МассивСообщений Цикл
			СообщениеОбОшибке.Сообщить();
		КонецЦикла;
		ПроизошлаОшибка = Истина;
		Если НЕ ФормаОткрыта Тогда
			ПриЗакрытииНаСервере(ИдентификаторЗадания);
			ЗакрытьФорму = Истина;
		Иначе
			Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ЕстьОшибка Тогда
		ПроизошлаОшибка = Истина;
		Если НЕ ФормаОткрыта Тогда
			ПриЗакрытииНаСервере(ИдентификаторЗадания);
			ЗакрытьФорму = Истина;
		Иначе
			Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если СтруктураВозврата.ТребуетсяПовторнаяАутентификация Тогда
		ПроизошлаОшибка = Ложь;
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("НастройкаОбмена", НастройкаОбмена);
		ПараметрыОбработки.Вставить("ПроцедураОбработчик", "ПродолжитьПолучениеВыпискиПослеПолученияМаркераБанка");
		ПараметрыАвторизации = Новый Структура;
		Если ОбменСБанкамиСлужебныйКлиент.ПолученыДанныеАвторизации(НастройкаОбмена, ПараметрыАвторизации) Тогда
			ОбработатьПолучениеДанныхАутентификации(ПараметрыАвторизации, ПараметрыОбработки);
		Иначе
			ОООЗ = Новый ОписаниеОповещения("ОбработатьПолучениеДанныхАутентификации", ЭтотОбъект, ПараметрыОбработки);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(НастройкаОбмена, ОООЗ);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ВыпискиБанка = ВыпискиПоЗапросам(МассивСообщенийОбменаСсылка);
	
	Если ВыпискиБанка = Неопределено Тогда // Произошла ошибка
		ПроизошлаОшибка = Истина;
		ОповеститьОВыборе(Неопределено);
	ИначеЕсли ВыпискиБанка.Количество() Тогда
		ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк'");
		ТекстОповещения = НСтр("ru = 'Получено документов: (%1).'");
		ТекстОповещения = СтрШаблон(ТекстОповещения, ВыпискиБанка.Количество());
		Оповестить("ОбновитьСостояниеОбменСБанками");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
		ОповеститьОВыборе(ВыпискиБанка);
	Иначе
		ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал = ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал
			* ПараметрыОжиданияПолученияВыписки.КоэффициентУвеличенияИнтервала;
		Если ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал > ПараметрыОжиданияПолученияВыписки.МаксимальныйИнтервал Тогда
			ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал = ПараметрыОжиданияПолученияВыписки.МаксимальныйИнтервал;
		КонецЕсли;
		
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьВыпискуАсинхронныйОбмен", ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал, Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыпискиПоЗапросам(Знач МассивЗапросовСсылка)
	
	МассивЗапросов = ПолучитьИзВременногоХранилища(МассивЗапросовСсылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СообщениеОбменСБанками.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	               |ГДЕ
	               |	СообщениеОбменСБанками.СообщениеРодитель В(&МассивЗапросов)
	               |	И СообщениеОбменСБанками.ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ВыпискаБанка)";
	Запрос.УстановитьПараметр("МассивЗапросов", МассивЗапросов);

	МассивВозврата = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() >= МассивЗапросов.Количество() Тогда
		Пока Выборка.Следующий() Цикл
			МассивВозврата.Добавить(Выборка.Ссылка);
		КонецЦикла;
	Иначе
		ЗапросОшибки = Новый Запрос;
		ЗапросОшибки.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	СообщениеОбменСБанками.Ссылка,
			|	СообщениеОбменСБанками.ПричинаОтклонения
			|ИЗ
			|	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
			|ГДЕ
			|	СообщениеОбменСБанками.Ссылка В(&МассивЗапросов)
			|	И СообщениеОбменСБанками.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.ОтклоненБанком)";
		ЗапросОшибки.УстановитьПараметр("МассивЗапросов", МассивЗапросов);
		Результат = ЗапросОшибки.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();

			ТекстСообщения = НСтр("ru = 'Запрос отклонен.
										|Причина отклонения: %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ПричинаОтклонения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатПолученияИзвещенияОСостоянииЭД()
		
	СтруктураВозврата = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	ОбменСБанкамиСлужебныйКлиент.ВызватьОповещения(СтруктураВозврата);

	Если СтруктураВозврата.Свойство("ДанныеЭП") И СтруктураВозврата.ДанныеЭП.Количество() Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПослеСохраненияПолученныхПодписейИзвещения", ЭтотОбъект);
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДанныеЭП", СтруктураВозврата.ДанныеЭП);
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Ложь);
		ДополнительныеПараметры.Вставить("ОповещениеПослеСохраненияПодписей", Оповещение);
		
		СохранитьОчереднуюПодпись(ДополнительныеПараметры);
		
		Возврат;

	КонецЕсли;
	
	ПослеСохраненияПолученныхПодписейИзвещения(Ложь, Неопределено)
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПолученныхПодписейИзвещения(ЕстьОшибка, ДополнительныеПараметры) Экспорт
	
	Если ЕстьОшибка Тогда
		ПроизошлаОшибка = Истина;
		Если НЕ ФормаОткрыта Тогда
			ПриЗакрытииНаСервере(ИдентификаторЗадания);
			ЗакрытьФорму = Истина;
		Иначе
			Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(АдресХранилища);

	ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк'");
	ТекстОповещения = НСтр("ru = 'Отправлено документов: (1).
								|Получено документов: (%1).'");
	ТекстОповещения = СтрШаблон(ТекстОповещения, СтруктураВозврата.КолПолученныхПакетов);
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
		
	МассивСообщений = ОбменСБанкамиСлужебныйВызовСервера.СообщенияОбОшибкахФоновогоЗадания(ИдентификаторЗадания);

	Если МассивСообщений.Количество() Тогда
		Для Каждого СообщениеОбОшибке Из МассивСообщений Цикл
			СообщениеОбОшибке.Сообщить();
		КонецЦикла;
		ПроизошлаОшибка = Истина;
		Если НЕ ФормаОткрыта Тогда
			ПриЗакрытииНаСервере(ИдентификаторЗадания);
			ЗакрытьФорму = Истина;
		Иначе
			Закрыть();
		КонецЕсли;
		Оповестить("ОбновитьСостояниеОбменСБанками");
		Возврат;
	КонецЕсли;
	
	Если СтруктураВозврата.ТребуетсяПовторнаяАутентификация Тогда
		ПроизошлаОшибка = Ложь;
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("НастройкаОбмена", НастройкаОбмена);
		ПараметрыОбработки.Вставить("ПроцедураОбработчик", "ПродолжитьПолучениеИзвещенияПослеПолученияМаркераБанка");
		ПараметрыАвторизации = Новый Структура;
		Если ОбменСБанкамиСлужебныйКлиент.ПолученыДанныеАвторизации(НастройкаОбмена, ПараметрыАвторизации) Тогда
			ОбработатьПолучениеДанныхАутентификации(ПараметрыАвторизации, ПараметрыОбработки);
		Иначе
			ОООЗ = Новый ОписаниеОповещения("ОбработатьПолучениеДанныхАутентификации", ЭтотОбъект, ПараметрыОбработки);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(НастройкаОбмена, ОООЗ);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	ПараметрыВозврата = Новый Структура("Успех", Истина);
		
	Если НЕ ФормаОткрыта Тогда
		Если НЕ ОписаниеОповещенияОЗакрытии = Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, ПараметрыВозврата);
		КонецЕсли;
		ЗакрытьФорму = Истина;
	Иначе
		Закрыть(ПараметрыВозврата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(Знач ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура ПолучитьВыпискуЧерезДополнительнуюОбработку()
	
	ВнешнийПодключаемыйМодуль = ОбменСБанкамиСлужебныйКлиент.ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(
		ИмяВнешнегоМодуля);
	ДанныеСертификата = ПолучитьИзВременногоХранилища(ДанныеСертификатаСсылка);
	
	ПараметрыВыписки = Новый Структура;
	ДатаНачалаСтрокой = Формат(ДатаНачала,    "ДЛФ=D");
	ДатаОкончанияСтрокой = Формат(ДатаОкончания, "ДЛФ=D");
	НазваниеЭД = НСтр("ru = 'Выписка банка за период с %1 по %2'");
	НазваниеЭД = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НазваниеЭД, ДатаНачалаСтрокой, ДатаОкончанияСтрокой);
	КолПолученных = 0;
	
	МассивНомеровБанковскихСчетов = ПолучитьИзВременногоХранилища(МассивБанковскихСчетовСсылка);
	СообщенияОбменаДляПроверки = Новый Массив;
	
	Для Каждого НомерСчета Из МассивНомеровБанковскихСчетов Цикл
		ПараметрыВыписки.Вставить("НомерСчета",        НомерСчета);
		ПараметрыВыписки.Вставить("БИК",               БИК);
		ПараметрыВыписки.Вставить("ДатаНачала"   ,     Формат(ДатаНачала,    "ДЛФ=D"));
		ПараметрыВыписки.Вставить("ДатаОкончания",     Формат(ДатаОкончания, "ДЛФ=D"));
		ПараметрыВыписки.Вставить("ВерсияСхемыДанных", ОбменСБанкамиКлиентСервер.ВерсияФорматаСинхронногоОбмена());
		ДанныеВыписки = ОбменСБанкамиСлужебныйКлиент.ОтправитьЗапросЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, 2, ПараметрыВыписки);
		Если ДанныеВыписки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого Подпись Из ДанныеВыписки.Подписи Цикл
			
			ДанныеСертификатаПодписи = ОбменСБанкамиСлужебныйКлиент.ДанныеСертификатаЧерезДополнительнуюОбработку(
				ВнешнийПодключаемыйМодуль, Подпись.Сертификат);
			Если ДанныеСертификатаПодписи = Неопределено Тогда
				ПроизошлаОшибка = Истина;
				Возврат;
			КонецЕсли;
			Подпись.Вставить("ДанныеСертификата", ДанныеСертификатаПодписи);
			
		КонецЦикла;
		
		СообщениеОбменаВыписка = СохранитьВыписку(ДанныеВыписки, НастройкаОбмена, НазваниеЭД);
		СообщенияОбменаДляПроверки.Добавить(СообщениеОбменаВыписка);
		ОбменСБанкамиСлужебныйВызовСервера.ОпределитьИсполненныеПлатежныеПоручения(СообщениеОбменаВыписка);
		КолПолученных = КолПолученных + 1;
	КонецЦикла;
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить(
		"МассивСообщенийОбменаДляПроверкиЧерезДополнительнуюОбработку", СообщенияОбменаДляПроверки);
	ПараметрыПроверки.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
	ПараметрыПроверки.Вставить("ТекущийИндексПроверкиПодписейЧерезДополнительнуюОбработку", 0);
	ПараметрыПроверки.Вставить("НастройкаОбмена", НастройкаОбмена);
	ОбменСБанкамиСлужебныйКлиент.НачатьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ПараметрыПроверки);
	
	ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк'");
			
	Если КолПолученных = 0 Тогда
		ТекстОповещения = НСтр("ru = 'Полученных документов нет.'");
	Иначе
		ТекстОповещения = НСтр("ru = 'Получено документов: (%1).'");
		ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОповещения, КолПолученных);
	КонецЕсли;
		
	Оповестить("ОбновитьСостояниеОбменСБанками");
		
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	
	ОповеститьОВыборе(СообщениеОбменаВыписка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СохранитьВыписку(Знач ДанныеВыписки, Знач НастройкаОбмена, Знач НазваниеЭД)
	
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаОбмена, "Организация, Банк");
	
	АдресФайла = ПоместитьВоВременноеХранилище(ДанныеВыписки.Выписка);
	
	ПараметрыСообщения = Новый Структура;
	ПараметрыСообщения.Вставить("Представление", НазваниеЭД);
	ПараметрыСообщения.Вставить("АдресФайлаВоВременномХранилище", АдресФайла);
	ПараметрыСообщения.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыСообщения.Вставить("ВидЭД", Перечисления.ВидыЭДОбменСБанками.ВыпискаБанка);
	ПараметрыСообщения.Вставить("Направление", Перечисления.НаправленияЭД.Входящий);
	ПараметрыСообщения.Вставить("Статус", Перечисления.СтатусыОбменСБанками.Получен);
	ПараметрыСообщения.Вставить("СсылкаНаОбъект", НастройкаОбмена);
	
	НовоеСообщение = Неопределено;
	ОбменСБанкамиСлужебный.СохранитьСообщениеОбмена(ПараметрыСообщения, НовоеСообщение);
	
	Для Каждого Подпись Из ДанныеВыписки.Подписи Цикл
		СтруктураСертификата = Новый Структура;
		СтруктураСертификата.Вставить("Отпечаток", Подпись.ДанныеСертификата.Отпечаток);
		СтруктураСертификата.Вставить("КомуВыдан", Подпись.ДанныеСертификата.ВладелецФИО);
		СтруктураСертификата.Вставить("ДвоичныеДанные", Подпись.Сертификат);
		ОбменСБанкамиСлужебныйВызовСервера.ДобавитьПодпись(НовоеСообщение, Подпись.Подпись, СтруктураСертификата);
	КонецЦикла;
	
	Возврат НовоеСообщение;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПолучениеДанныхАутентификации(ДанныеАутентификации, ПараметрыОбработки) Экспорт
	
	Если ДанныеАутентификации = Неопределено Тогда
		ПроизошлаОшибка = Ложь;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	РеквизитыНастройкиОбмена = Новый Структура("АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	Обработчик = Новый ОписаниеОповещения(ПараметрыОбработки.ПроцедураОбработчик, ЭтотОбъект, ПараметрыОбработки);
	ДанныеАутентификации.Вставить("Пароль", ДанныеАутентификации.ПарольПользователя);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
		РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ДанныеАутентификации, РеквизитыНастройкиОбмена.ВерсияФормата,
		НастройкаОбмена);

КонецПроцедуры
	
&НаКлиенте
Процедура ПродолжитьПолучениеВыпискиПослеПолученияМаркераБанка(Маркер, ПараметрыОбработки) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		ПроизошлаОшибка = Ложь;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса.ИдентификаторСессии = Маркер;
	
	ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал = ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал
		* ПараметрыОжиданияПолученияВыписки.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал > ПараметрыОжиданияПолученияВыписки.МаксимальныйИнтервал Тогда
		ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал = ПараметрыОжиданияПолученияВыписки.МаксимальныйИнтервал;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания(
		"Подключаемый_ПолучитьВыпискуАсинхронныйОбмен", ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПолучениеИзвещенияПослеПолученияМаркераБанка(Маркер, ПараметрыОбработки) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		ПроизошлаОшибка = Ложь;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса.ИдентификаторСессии = Маркер;
	ОперацияВыполнена = ОбменСБанкамиСлужебныйВызовСервера.ПолучитьИзвещениеОСостоянииЭДАсинхронноНаСервере(
		ПараметрыЗапроса, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания);
	
	Если ОперацияВыполнена Тогда
		ОбработатьРезультатПолученияИзвещенияОСостоянииЭД();
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеПолученияИзвещенияОСостоянии", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуНаКлиенте(ПараметрЗакрытия = Неопределено)
	
	Если НЕ ФормаОткрыта Тогда
		ЗакрытьФорму = Истина;
	Иначе
		Закрыть(ПараметрЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

#Область ОбменЧерезВК

&НаКлиенте
Процедура ПолучитьВыпискуЧерезВК()
	
	ДополнительныеПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьНовыеДокументыПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьИИнициализироватьВК(Оповещение, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНовыеДокументыПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	ИначеЕсли Результат = Неопределено Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = Результат;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат);
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияНовыхДокументовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПолучитьНовыеДокументыВК(Оповещение, Результат, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСостояниеЭДЧерезВК()
	
	ДополнительныеПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьНовыеДокументыПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьИИнициализироватьВК(Оповещение, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияНовыхДокументовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.Результат);
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Результат.Результат = Неопределено ИЛИ Результат.Результат = Ложь Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если ОбменСБанкамиСлужебныйКлиент.ПроцессПрерван(НастройкаОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбмена) Тогда
		Извещение = Неопределено;
		ИзвещениеПолучено = ОбменСБанкамиСлужебныйВызовСервера.ПолученоИзвещениеПоЗапросу(СообщениеОбмена, ПроизошлаОшибка);
		
		Если ПроизошлаОшибка ИЛИ ИзвещениеПолучено Тогда
			ЗакрытьФормуНаКлиенте();
			Возврат;
		КонецЕсли;
		
		ПолучитьНовыеДокументыПослеПодключенияВК(ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры);
		
	Иначе
	
		ВыпискиБанка = ВыпискиПоЗапросам(МассивСообщенийОбменаСсылка);
	
		Если ВыпискиБанка = Неопределено Тогда // Произошла ошибка
			ПроизошлаОшибка = Истина;
			ЗакрытьФормуНаКлиенте();
		ИначеЕсли ВыпискиБанка.Количество() Тогда
			ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк'");
			ТекстОповещения = НСтр("ru = 'Получено документов: (%1).'");
			ТекстОповещения = СтрШаблон(ТекстОповещения, ВыпискиБанка.Количество());
			Оповестить("ОбновитьСостояниеОбменСБанками");
			ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
			ОповеститьОВыборе(ВыпискиБанка);
		Иначе
			ПолучитьНовыеДокументыПослеПодключенияВК(ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Сбербанк

&НаКлиенте
Процедура ПослеОтправкиОчередногоЗапросаВыпискиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;

	МассивТикетовСбербанк.Добавить(Результат);
	
	ОтправитьЗапросыВыписокРекурсивноСбербанк(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаСтатусаЗапросаСостоянияСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"ПолучитьИзвещениеОСостоянииДокументаСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ПроизошлаОшибка = Истина;
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка. Повторите операцию позже.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ЗакрытьФормуНаКлиенте();
		Возврат;
	Иначе
		МассивНовыхСообщенийОбмена = Новый Массив;
		ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
			Ответ, НастройкаОбмена, МассивНовыхСообщенийОбмена, ИсходныйТикетСбербанк, ПроизошлаОшибка);
		Если ПроизошлаОшибка Тогда
			ЗакрытьФормуНаКлиенте();
			Возврат;
		КонецЕсли;
		Оповещение = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
		Оповестить("ОбновитьСостояниеОбменСБанками");
		ЗакрытьФормуНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтветаПоТикетуЗапросаНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	МассивНовыхСообщенийОбмена = Новый Массив;
	ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
		Результат, НастройкаОбмена, МассивНовыхСообщенийОбмена, , ПроизошлаОшибка);
	Если ПроизошлаОшибка Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
		ИтогКолПолученных = ИтогКолПолученных + МассивНовыхСообщенийОбмена.Количество();
		Оповестить("ОбновитьСостояниеОбменСБанками");
	КонецЕсли;

	МассивТикетовСбербанк = Новый Массив;
	ТекстЗапросаСостоянияПлатежныеДокументов = ТекстЗапросаСостоянияОбработкиПлатежныхДокументовСбербанк(
		НастройкаОбмена, МассивТикетовСбербанк);
	
	Если ЗначениеЗаполнено(ТекстЗапросаСостоянияПлатежныеДокументов) Тогда
		ТикетСбербанк = Неопределено;
		
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаСостоянияПлатежныхДокументов", ЭтотОбъект);
		
		ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхСбербанк(
			Оповещение, ТекстЗапросаСостоянияПлатежныеДокументов, НастройкаОбмена);
		Возврат;
	КонецЕсли;
	
	Если МассивТикетовСбербанк.Количество() = 0 Тогда
		ЗакрытьФормуНаКлиенте();
	Иначе
		НачатьПолучениеДокументовПоТикетамСбербанк();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтветаПоТикетуЗапросовВыпискиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	МассивНовыхСообщенийОбмена = Новый Массив;
	ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
		Результат, НастройкаОбмена, МассивНовыхСообщенийОбмена, , ПроизошлаОшибка);
	Если ПроизошлаОшибка Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
		Оповестить("ОбновитьСостояниеОбменСБанками");
	КонецЕсли;

	ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(
		ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк);
	ПодключитьОбработчикОжидания("ПолучитьКонечныеСтатусыЗапросовВыпискиСбербанк",
		ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтветаПоТикетуСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	МассивНовыхСообщенийОбмена = Новый Массив;
	ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
		Результат, НастройкаОбмена, МассивНовыхСообщенийОбмена, , ПроизошлаОшибка);
	Если ПроизошлаОшибка Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповестить("ОбновитьСостояниеОбменСБанками");
		Оповещение = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
	КонецЕсли;

	ОбменСБанкамиСлужебныйВызовСервера.УдалитьИдентификаторЗапроса(НастройкаОбмена, ТикетСбербанк);
	
	НачатьПолучениеДокументовПоТикетамСбербанк()

КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОчередногоСтатусаЗапросаСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ДополнительныеПараметры.МассивТикетовСбербанкКопия.Добавить(ДополнительныеПараметры.Тикет);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка. Повторите операцию позже.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	Иначе
		ЕстьОшибка = Ложь;
		МассивНовыхСообщенийОбмена = Новый Массив;
		ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
			Ответ, НастройкаОбмена, МассивНовыхСообщенийОбмена, , ЕстьОшибка);
		Если ЕстьОшибка Тогда
			ПроизошлаОшибка = Истина;
			ЗакрытьФормуНаКлиенте();
			Возврат;
		КонецЕсли;
		
		Если МассивНовыхСообщенийОбмена.Количество() Тогда
			Оповещение = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
			ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
			Оповестить("ОбновитьСостояниеОбменСБанками");
		КонецЕсли;
	КонецЕсли;

	ПолучитьСтатусыЗапросовРекурсивноСбербанк(ДополнительныеПараметры)
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияСтатусаЗапросаСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Ответ) Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка. Повторите операцию позже.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	Иначе // Ответ получен
		ВыполнитьОбработкуОповещения(ОповещениеПослеПолученияОтветаПоТикетуСбербанк, Ответ);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаСтатусаЗапросаВыпискиСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ТикетСбербанк = Ответ;
	
	ОповещениеПослеПолученияОтветаПоТикетуСбербанк = Новый ОписаниеОповещения(
		"ПослеПолученияОтветаПоТикетуЗапросовВыпискиСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаСостоянияПлатежныхДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	МассивТикетовСбербанк.Добавить(Результат);
	НачатьПолучениеДокументовПоТикетамСбербанк();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыпискиПолученыСбербанк(Знач НастройкаОбмена, Знач ЗапросыВыписокСсылка, Знач ДатаНачала, Знач ДатаОкончания, Знач БанковскиеСчетаСсылка, Знач ДатаНачалаОперации, ВыпискиБанка)

	МассивЗапросов = ПолучитьИзВременногоХранилища(ЗапросыВыписокСсылка);
	МассивБанковскихСчетов = ПолучитьИзВременногоХранилища(БанковскиеСчетаСсылка);
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	1 КАК Поле1
	               |ИЗ
	               |	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	               |ГДЕ
	               |	СообщениеОбменСБанками.Ссылка В(&МассивСсылок)
	               |	И СообщениеОбменСБанками.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.Обработан)";
	Запрос.УстановитьПараметр("МассивСсылок", МассивЗапросов);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда // есть хоть один запрос, по которому еще не пришла выписка
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СообщениеОбменСБанками.Ссылка,
	               |	СообщениеОбменСБанками.ДатаСообщения КАК ДатаСообщения,
	               |	СообщениеОбменСБанками.НомерСчета КАК НомерСчета,
	               |	СообщениеОбменСБанками.Дата
	               |ИЗ
	               |	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	               |ГДЕ
	               |	СообщениеОбменСБанками.ДатаСообщения >= &ДатаНачала
	               |	И СообщениеОбменСБанками.ДатаСообщения <= &ДатаОкончания
	               |	И СообщениеОбменСБанками.НастройкаОбмена = &НастройкаОбмена
	               |	И СообщениеОбменСБанками.ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ВыпискаБанка)
	               |	И СообщениеОбменСБанками.НомерСчета В(&МассивСчетов)
	               |	И СообщениеОбменСБанками.Дата >= &ДатаНачалаОперации
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ДатаСообщения УБЫВ,
	               |	НомерСчета";
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
	Запрос.УстановитьПараметр("МассивСчетов", МассивБанковскихСчетов);
	Запрос.УстановитьПараметр("ДатаНачалаОперации", ДатаНачалаОперации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТекСчет = Неопределено; ТекДата = Неопределено;
	ВыпискиБанка = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Если Выборка.ДатаСообщения <> ТекДата ИЛИ Выборка.НомерСчета <> ТекСчет Тогда
			ТекДата = Выборка.ДатаСообщения;
			ТекСчет = Выборка.НомерСчета;
			ВыпискиБанка.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ДанныеЗапросовВыписокСбербанк(МассивСообщенийОбмена)
	
	СоответствиеВозврата = Новый Соответствие;
	
	Для Каждого СообщениеОбмена Из МассивСообщенийОбмена Цикл
		ПакетXML = ОбменСБанкамиСлужебныйВызовСервера.ПакетXMLСбербанка(СообщениеОбмена, НастройкаОбмена);
		СоответствиеВозврата.Вставить(СообщениеОбмена, ПакетXML);
	КонецЦикла;
	
	Возврат СоответствиеВозврата;
	
КонецФункции

&НаКлиенте
Процедура НачатьОтправкуЗапросовВыписокСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьЗапросыВыпискиПослеАутентификацииНаСервереСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.УстановитьСоединениеИАутентифицироватьсяНаСервереСбербанк(Оповещение, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеДокументовПоТикетамСбербанк()
	
	Если МассивТикетовСбербанк.Количество() = 0 Тогда // все ответы получены
		ПараметрЗакрытия = Новый Структура("ИтогКолОтправленных, ИтогКолПолученных", ИтогКолОтправленных, ИтогКолПолученных);
		ЗакрытьФормуНаКлиенте(ПараметрЗакрытия);
		Возврат;
	КонецЕсли;
	
	ТикетСбербанк = МассивТикетовСбербанк[0];
	МассивТикетовСбербанк.Удалить(0);
		
	ОповещениеПослеПолученияОтветаПоТикетуСбербанк = Новый ОписаниеОповещения(
		"ПослеПолученияОтветаПоТикетуСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьВыпискуСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьОтветПоТикетуПослеОтправкиЗапросаНовыхДокументовСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ОтправитьЗапросНовыхДокументовСбербанк(
		Оповещение, НастройкаОбмена, СертификатПодписиСбербанк, ИдентификаторСертификатаСбербанк);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросыВыпискиПослеАутентификацииНаСервереСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	СоответствиеТекстовЗапросов = ПолучитьИзВременногоХранилища(ТекстыЗапросовВыписокСбербанк);
	МассивТекстовЗапросов = Новый Массив;
	МассивТикетовСбербанк = Новый Массив;
	
	Для Каждого КлючЗначение Из СоответствиеТекстовЗапросов Цикл
		МассивТекстовЗапросов.Добавить(КлючЗначение.Значение);
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивТекстовЗапросов", МассивТекстовЗапросов);
	ДополнительныеПараметры.Вставить("СоответствиеТекстовЗапросов", СоответствиеТекстовЗапросов);
	
	ОтправитьЗапросыВыписокРекурсивноСбербанк(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросыВыписокРекурсивноСбербанк(ДополнительныеПараметры)
	
	Если НЕ ДополнительныеПараметры.МассивТекстовЗапросов.Количество() Тогда // все запросы отправлены
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"ПолучитьВнешниеИдентификаторыЗапросовВыпискиСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Возврат;
	КонецЕсли;
	
	ТекущийТекстЗапроса = ДополнительныеПараметры.МассивТекстовЗапросов.Получить(0);
	ДополнительныеПараметры.МассивТекстовЗапросов.Удалить(0);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтправкиОчередногоЗапросаВыпискиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхСбербанк(Оповещение, ТекущийТекстЗапроса, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВнешниеИдентификаторыЗапросовВыпискиСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("МассивТикетовСбербанкКопия", Новый Массив);
	
	ПолучитьСтатусыЗапросовРекурсивноСбербанк(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВыпискуПослеПолученияОтветаПоТикетуЗапросаНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	МассивНовыхСообщенийОбмена = Новый Массив;

	ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
		Результат, НастройкаОбмена, МассивНовыхСообщенийОбмена, , ПроизошлаОшибка);
	
	Если ПроизошлаОшибка Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;

	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеПроверкиПодписиВыпискиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
		Оповестить("ОбновитьСостояниеОбменСБанками");
	Иначе
		ПослеПроверкиПодписиВыпискиСбербанк(Результат, ДополнительныеПараметры)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиПодписиВыпискиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	ВыпискиБанка = Неопределено;
	ПолученыВсеВыписки = ВыпискиПолученыСбербанк(НастройкаОбмена, МассивСообщенийОбменаСсылка, ДатаНачала, ДатаОкончания,
		МассивБанковскихСчетовСсылка, ДатаНачалаОперацииСбербанк, ВыпискиБанка);
	
	Если ПолученыВсеВыписки Тогда
		ОповеститьОВыборе(ВыпискиБанка);
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("ОжидатьВыпискуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИзвещениеОСостоянииДокументаСбербанк()
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаСтатусаЗапросаСостоянияСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(
		Оповещение, ИдентификаторОрганизации, ИсходныйТикетСбербанк, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКонечныеСтатусыЗапросовВыпискиСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьОшибка = Ложь;
	ТекстЗапроса = ТекстЗапросаСостоянияЗапросовВыписокСбербанк(НастройкаОбмена, МассивСообщенийОбменаСсылка, ЕстьОшибка);
	
	Если ЕстьОшибка Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстЗапроса) Тогда // есть запросы в не конечном статусе
		ТикетСбербанк = Неопределено;
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаСтатусаЗапросаВыпискиСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхСбербанк(Оповещение, ТекстЗапроса, НастройкаОбмена);
	Иначе // все запросы выписок имеют конечный статус
		Оповещение = Новый ОписаниеОповещения("ПослеОпределенияСертификатаСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСертификатПодписиСбербанк(Оповещение, НастройкаОбмена);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНовыеДокументыСбербанк()

	ТикетСбербанк = ИсходныйТикетСбербанк;
	ОповещениеПослеПолученияОтветаПоТикетуСбербанк = Новый ОписаниеОповещения(
		"ПослеПолученияОтветаПоТикетуЗапросаНовыхДокументовСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОтветПоТикетуПослеОтправкиЗапросаНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ТикетСбербанк = Результат.Тикет;
	
	ОповещениеПослеПолученияОтветаПоТикетуСбербанк = Новый ОписаниеОповещения(
		"ПолучитьВыпискуПослеПолученияОтветаПоТикетуЗапросаНовыхДокументовСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОтветПоТикетуСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияСтатусаЗапросаСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(
		Оповещение, ИдентификаторОрганизации, ТикетСбербанк, НастройкаОбмена);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСтатусыЗапросовРекурсивноСбербанк(ДополнительныеПараметры)
	
	Если НЕ МассивТикетовСбербанк.Количество() Тогда
		
		Если ДополнительныеПараметры.МассивТикетовСбербанкКопия.Количество() Тогда
			МассивТикетовСбербанк = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(
				ДополнительныеПараметры.МассивТикетовСбербанкКопия);
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"ПолучитьВнешниеИдентификаторыЗапросовВыпискиСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Иначе
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(
				ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк);
			ПодключитьОбработчикОжидания("ПолучитьКонечныеСтатусыЗапросовВыпискиСбербанк",
				ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк.ТекущийИнтервал, Истина);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Тикет = МассивТикетовСбербанк.Получить(0);
	МассивТикетовСбербанк.Удалить(0);
	ДополнительныеПараметры.Вставить("Тикет", Тикет);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеПолученияОчередногоСтатусаЗапросаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(
		Оповещение, ИдентификаторОрганизации, Тикет, НастройкаОбмена);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстЗапросаСостоянияЗапросовВыписокСбербанк(Знач НастройкаОбмена, Знач МассивСообщенийОбменаСсылка, ЕстьОшибка)
	
	ТекстВозврата = "";
	
	МассивСообщенийОбмена = ПолучитьИзВременногоХранилища(МассивСообщенийОбменаСсылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СообщениеОбменСБанками.Статус,
	               |	СообщениеОбменСБанками.ПричинаОтклонения,
	               |	СообщениеОбменСБанками.ВнешнийИдентификатор
	               |ИЗ
	               |	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	               |ГДЕ
	               |	СообщениеОбменСБанками.Ссылка В(&МассивСообщенийОбмена)
	               |	И НЕ СообщениеОбменСБанками.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.Исполнен)";
	
	Запрос.УстановитьПараметр("МассивСообщенийОбмена", МассивСообщенийОбмена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивИдентификаторов = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Статус = Перечисления.СтатусыОбменСБанками.ОтклоненБанком Тогда
			ТекстСообщения = НСтр("ru = 'Запрос выписки отклонен по причине: %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ПричинаОтклонения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , ЕстьОшибка);
			Прервать;
		КонецЕсли;
		МассивИдентификаторов.Добавить(Выборка.ВнешнийИдентификатор);
	КонецЦикла;
	
	Если МассивИдентификаторов.Количество() Тогда
		ТекстВозврата = ОбменСБанкамиСлужебный.ТекстЗапросаСостоянияДокументовСбербанк(
			НастройкаОбмена, МассивИдентификаторов);
		Если Не ЗначениеЗаполнено(ТекстВозврата) Тогда
			ЕстьОшибка = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстВозврата;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаСостоянияОбработкиПлатежныхДокументовСбербанк(Знач НастройкаОбмена, МассивТикетов)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СообщениеОбменСБанками.ВнешнийИдентификатор
	|ИЗ
	|	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОбменСБанками КАК СостоянияОбменСБанками
	|		ПО (СостоянияОбменСБанками.СообщениеОбмена = СообщениеОбменСБанками.Ссылка)
	|ГДЕ
	|	СообщениеОбменСБанками.НастройкаОбмена = &НастройкаОбмена
	|	И СообщениеОбменСБанками.ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение)
	|	И СостоянияОбменСБанками.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОбменСБанками.ОжидаетсяИсполнение)
	|	И НЕ СообщениеОбменСБанками.ВнешнийИдентификатор = """"
	|	И НЕ СообщениеОбменСБанками.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТикетыОбменСБанками.Идентификатор
	|ИЗ
	|	РегистрСведений.ТикетыОбменСБанками КАК ТикетыОбменСБанками
	|ГДЕ
	|	ТикетыОбменСБанками.НастройкаОбмена = &НастройкаОбмена";
	
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИдентификаторыДокументов = РезультатЗапроса[0].Выгрузить().ВыгрузитьКолонку("ВнешнийИдентификатор");
	МассивТикетов = РезультатЗапроса[1].Выгрузить().ВыгрузитьКолонку("Идентификатор");
	
	Если ИдентификаторыДокументов.Количество() Тогда
		Возврат ОбменСБанкамиСлужебный.ТекстЗапросаСостоянияДокументовСбербанк(НастройкаОбмена, ИдентификаторыДокументов);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ПослеОпределенияСертификатаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте();
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда // Пользователь не выбрал сертификат
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	СертификатПодписиСбербанк = Результат.СертификатСсылка;
	ИдентификаторСертификатаСбербанк = Результат.ИдентификаторСертификата;
	
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ОжидатьВыпискуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Инициализация

ИтогКолОтправленных = 0;
ИтогКолПолученных = 0;

#КонецОбласти
